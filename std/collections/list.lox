// A list of elements.
class List {
	// Initialize the list.
	init() {
		// The number of elements in the list.
		this._length = 0;
		
		// The front node of the list.
		this._head = nil;
		
		// The back node of the list.
		this._tail = nil;
	}
	
	// Get the number of elements in the list.
	getLength() {
		return this._length;
	}
	
	// Append an element to the back of the list.
	pushBack(elem) {
		var tail = this._tail;
		var node = ListNode(elem, tail, nil);
		
		if (tail) {
			tail.next = node;
		} else {
			this._head = node;
		}
		
		this._tail = node;
		this._length = this._length + 1;
	}
	
	// Create an iterator for the front of the list.
	iter() {
		return ListIterator(this._head);
	}
}
